# 컨트랙트 생성 컨트랙트  

솔리디티는 스마트 컨트랙트를 통해 다른 컨트랙트를 생성할 수 있습니다. 

예제 코드를 통해 이해해보도록 합시다. 

## 예제 코드 

```solidity
// SPDX-License-Identifier: MIT
pragma solidity ^0.8.24;


contract Car {
    address public owner;
    string public model;
    address public carAddr;

    constructor(address _owner, string memory _model) payable {
        owner = _owner;
        model = _model;
        carAddr = address(this);
    }
}

contract CarFactory {
    Car[] public cars;

    function create(address _owner, string memory _model) public {
        Car car = new Car(_owner, _model);
        cars.push(car);
    }

    function createAndSendEther(address _owner, string memory _model)
        public
        payable
    {
        Car car = (new Car){value: msg.value}(_owner, _model);
        cars.push(car);
    }

    function getCar(uint256 _index)
        public
        view
        returns (
            address owner,
            string memory model,
            address carAddr,
            uint256 balance
        )
    {
        Car car = cars[_index];

        return (car.owner(), car.model(), car.carAddr(), address(car).balance);
    }
}
```

###  Car 컨트랙트 생성 
생성자로 owner와 model을 인자로 받는 Car 컨트랙트를 생성해줍니다.  

### CarFactory 컨트랙트 생성 
CarFactory 컨트랙트에서 Car 컨트랙트를 생성하고 생성된 컨트랙트들을 cars 변수에 저장할 수 있습니다.  

### create 함수 
CarFactory 컨트랙트에서 new 키워드를 통해 car을 생성하고 cars에 이를 푸쉬합니다.  

### createAndSendEther 함수 
컨트랙트를 **생성** 하는 것이기 때문에 컨트랙트에 송금하는 것도 가능합니다.  

### getCar 함수 
getCar 함수를 통해 생성된 컨트랙트의 owner, model, carAddr, balance를 조회할 수 있습니다. 

## Remix에서 실습

1. Remix에서 새로운 solidity 파일 생성해서 예제 코드를 복사 붙여넣기 합니다.
2. 예제 코드를 compile 후 deploy합니다.
3. 아래 버튼들이 제대로 동작하는지 확인합니다.  


![image](https://github.com/mmingyeomm/solidity_basic_module-/assets/87323564/8f2e8810-a56d-493a-a86a-e77830e40c66)



